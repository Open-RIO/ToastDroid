buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven {
        name = "Jaci"
        url = "http://dev.imjac.in/maven"
    }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "jaci.openrio.module.android"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
	
	signingConfigs {
		release {
			storeFile file("someFalseFile.keystore")
			storePassword "someFalsePassword"
			keyAlias "someFalseAlias"
			keyPassword "someFalsePassword"
		}
	}
	
    buildTypes {
        release {
            minifyEnabled false
			signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task loadProps << {
	def config = new Properties()
	def propFile = file("../local.properties")
	if (propFile.canRead()) {
		config.load(new FileInputStream(propFile))
		android.signingConfigs.release.storeFile = file(config["release.key.store"])
        android.signingConfigs.release.storePassword = config["release.key.store.password"]
        android.signingConfigs.release.keyAlias = config["release.key.alias"]
        android.signingConfigs.release.keyPassword = config["release.key.password"]
		def keyFile = file(config["release.key.store"])
		println "Building with Alias: ${config['release.key.alias']} at ${keyFile.getAbsolutePath()}"
	}
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease") || theTask.name.equals("packageBeta")) {
        theTask.dependsOn "loadProps"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:cardview-v7:22.1.1'
    compile 'com.android.support:gridlayout-v7:22.1.1'
    compile 'com.android.support:support-v4:22.1.1'
    compile 'jaci.openrio.delegate:NetworkDelegate:+'
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.0'
}
